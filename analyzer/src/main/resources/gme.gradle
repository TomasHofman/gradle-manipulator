/**
 * This file is injected by the Gradle Manipulation Tool during the analysis phase
 */


// TODO: Do we need AProxDependencyUrl in here and init script?
buildscript {
    if (System.getenv("AProxDependencyUrl") != null) {
        // Running inside PNC
        repositories {
            maven {
                name "indy-mvn"
                url System.getenv("AProxDependencyUrl")
                credentials(HttpHeaderCredentials) {
                    name = "Authorization"
                    value = "Bearer " + System.getenv('accessToken')
                }
                authentication { header(HttpHeaderAuthentication) }
            }
        }
    } else {
        // Running elsewhere e.g. local development environment.
        repositories {
            // Maven local isn't required for production but its useful during development.
            mavenLocal()
            mavenCentral()
            maven {
                url "https://oss.sonatype.org/content/repositories/snapshots"
            }
        }
    }

    dependencies {
        classpath "org.jboss.gm.manipulation:manipulation:${project.version}"
    }
}

allprojects {
    // If we are not running the analyse plugin then apply the manipulation plugin.
    // This avoids issues with rerunning in local environments.
    if (!project.hasProperty("gmeAnalyse")) {
        logger.info("Applying plugin org.jboss.gm.manipulation.ManipulationPlugin")
        apply plugin: org.jboss.gm.manipulation.ManipulationPlugin
    }

}


// Extra configuration for badly behaved plugins
pluginManager.withPlugin("dokka") {
    // TODO: Investigate whether proxy could avoid this for dokka.
    //
    // Disable linking to online kotlin-stdlib documentation
    noStdlibLink = true
    // Disable linking to online JDK documentation
    noJdkLink = true
    // Disable any user-configured external links.
    externalDocumentationLinks.clear()
}
